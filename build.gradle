import java.time.OffsetDateTime

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'com.rikonardo.papermake' version '1.0.6'
    id 'xyz.jpenilla.run-velocity' version '2.3.1'
}

allprojects {
    apply plugin: 'java'

    group = 'cx.rain.mc'
    version = '1.1.1'

    repositories {
        mavenCentral()
        maven {
            name = 'spigotmc-repo'
            url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
        }
        maven {
            name = 'PaperMC repo'
            url = 'https://repo.papermc.io/repository/maven-snapshots/'
        }
        maven {
            name = 'dmulloy2-repo'
            url = 'https://repo.dmulloy2.net/repository/public/'
        }
        mavenLocal()
        maven {
            name = 'Yuluo Maven'
            url = 'https://maven.yuluo.dev/repository/maven-public/'
        }
        maven {
            name = 'CodeMC NMS repo'
            url = 'https://repo.codemc.io/repository/nms/'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/groups/public/'
        }
    }

    processResources {
        def props = [
                plugin_version: version,
                build_time: OffsetDateTime.now()
        ]
        def files = [
                'build_info.properties',
                'plugin.yml'
        ]

        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching(files) {
            expand props
        }
    }

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        options.release = 17
    }
}

dependencies {
    implementation project(':common')
    implementation project(':spigot')
    implementation project(':velocity')
}

jar.finalizedBy(shadowJar)

runVelocity {
    velocityVersion '3.4.0-SNAPSHOT'
}
